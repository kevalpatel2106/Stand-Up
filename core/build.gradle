/*
 *  Copyright 2018 Keval Patel.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 */

apply plugin: 'com.android.library'
Plugins.modulePlugins.each { plugins.apply(it) }

android {
    signingConfigs signingConfig

    compileSdkVersion buidlConfig.compile
    buildToolsVersion buidlConfig.buildToolVersion

    defaultConfig libDefaultConfig

    buildTypes {
        release {
            resValue "string", "application_name", DEBUG_APPLICATION_NAME
            signingConfig signingConfigs.playStore
        }
        debug {
            crunchPngs false
            testCoverageEnabled true
            signingConfig signingConfigs.debugKey
            resValue "string", "application_name", RELEASE_APPLICATION_NAME
        }
    }

    compileOptions compileOption
    packagingOptions packageExcludes
    lintOptions lintOption
    aaptOptions aaptOption
    dexOptions dexOption
    testOptions testOption
    adbOptions adbOption
}

dokka dokkaConfig
kotlin kotlinOption

dependencies {
    implementation project(':utils')
    implementation project(':common')
    testImplementation project(':test-utils')
    testImplementation project(':network')
    androidTestImplementation project(':test-utils')
    testImplementation(Deps.roboElectric, excludeGrp)

    ide.each { add("implementation", it) }
    rxLib.each { add("implementation", it) }
    debugLibs.each { add(it.configuration, it.dependency) }
    dagger.each { add(it.configuration, it.dependency) }

    //Support
    implementation Deps.appCompat
    implementation Deps.supportv4

    implementation(playServiceLib.location, excludeGrp)
    implementation(playServiceLib.awareness, excludeGrp)

    implementation(Deps.evernoteJob, excludeGrp)
    implementation Deps.gson
    implementation Deps.retrofit
}
