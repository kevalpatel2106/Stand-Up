/*
 *  Copyright 2018 Keval Patel.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
Plugins.modulePlugins.each { plugins.apply(it) }
//https://stackoverflow.com/a/47050632/4690731
//apply plugin: 'com.google.firebase.firebase-perf'

android {
    signingConfigs signingConfig
    compileSdkVersion DefaultConfig.compile
    buildToolsVersion DefaultConfig.buildToolVersion
    defaultConfig defaultAppConfig

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            crunchPngs false
            testCoverageEnabled false   //https://stackoverflow.com/a/47050632/4690731
            debuggable true
            jniDebuggable true
            renderscriptDebuggable false
            signingConfig signingConfigs.debugKey
            minifyEnabled false
            zipAlignEnabled true

            buildConfigField "String", "BASE_URL", "\"" + DebugConfig.SERVER_BASE_URL + "\""
            resValue "string", "application_name", DebugConfig.APPLICATION_NAME
            resValue "string", "package_name", DebugConfig.PACKAGE_NAME
            resValue "string", "api_host", "\"" + DebugConfig.SERVER_BASE_URL.replace("https://", "") + "\""
            resValue "string", "android_api_key", DebugConfig.ANDROID_API_KEY
            resValue "string", "facebook_app_id", DebugConfig.FACEBOOK_APP_ID
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), ProguardFiles.proguard, ProguardFiles.removeLogs
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.playStore
            shrinkResources true
            pseudoLocalesEnabled false
            zipAlignEnabled true

            buildConfigField "String", "BASE_URL", "\"" + ReleaseConfig.RELEASE_SERVER_BASE_URL + "\""
            resValue "string", "application_name", ReleaseConfig.RELEASE_APPLICATION_NAME
            resValue "string", "package_name", ReleaseConfig.RELEASE_PACKAGE_ID
            resValue "string", "api_host", "\"" + ReleaseConfig.RELEASE_SERVER_BASE_URL.replace("https://", "") + "\""
            resValue "string", "android_api_key", ReleaseConfig.RELEASE_ANDROID_API_KEY
            resValue "string", "facebook_app_id", ReleaseConfig.RELEASE_FACEBOOK_APP_ID
        }
    }

    compileOptions compileOption
    packagingOptions packageExcludes
    lintOptions lintOption
    aaptOptions aaptOption
    dexOptions dexOption
    testOptions testOption
    adbOptions adbOption
    dexcount dexcountOptions
}

dokka dokkaConfig
kotlin kotlinOption

dependencies {
    implementation project(':common')
    implementation project(':feature-authentication')
    implementation project(':feature-dashboard')
    implementation project(':feature-diary')
    implementation project(':feature-about')
    implementation project(':feature-profile')
    implementation project(':feature-settings')
    implementation project(':feature-stats')
    implementation project(':utils')
    implementation project(':network')
    implementation project(':core')
    implementation project(':progress-button')
    implementation project(':timeline-view')

    //Test dependency
    testImplementation project(':test-utils')
    androidTestImplementation project(':test-utils')

    implementation(Deps.kotlinRefflect)
    implementation(Deps.kotlinStdLib)
    implementation(Deps.constraintLayout)

    implementation(Deps.rxJava)
    implementation(Deps.rxAndroid)

    implementation(Deps.timber)
    debugImplementation(Deps.debugDb)
    debugImplementation(Deps.leakCanary)

    //Dagger
    implementation(Deps.daggerAndroid)
    implementation(Deps.daggerAndroidSupport)
    kapt(Deps.daggerAnnotations)
    kapt(Deps.daggerCompiler)
    compileOnly(Deps.jsr250)

    //Support
    implementation Deps.appCompat
    implementation Deps.supportv4
    implementation Deps.multidex
    implementation Deps.design

    //Arch component
    implementation Deps.lifecycle

    //Firebase
    implementation(Deps.firebasePreformance, excludeGrp)
    implementation(Deps.firebaseCore, excludeGrp)
    implementation(Deps.fcm, excludeGrp)

    //Glide
    implementation(Deps.glide, excludeGrp)
    kapt(Deps.glideAnnotation, excludeGrp)

    //Network dependencies
    implementation Deps.retrofit
    implementation Deps.gson

    //Other libs
    implementation(Deps.circleImagView, excludeGrp)
    implementation(Deps.iap, excludeGrp)
    implementation(Deps.facebook, excludeGrp)
    releaseImplementation(Deps.crashalytics) { transitive = true }
}
apply plugin: 'com.google.gms.google-services'
