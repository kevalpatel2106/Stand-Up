/*
 *  Copyright 2017 Keval Patel.
 *
 *  Licensed under the GNU General Public License, Version 3.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  https://www.gnu.org/licenses/gpl-3.0.en.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
//https://stackoverflow.com/a/47050632/4690731
//apply plugin: 'com.google.firebase.firebase-perf'

apply from: '../gradle/config.gradle'
apply from: '../gradle/deps.gradle'

android {
    signingConfigs signingConfig
    compileSdkVersion buidlConfig.compile
    buildToolsVersion buidlConfig.buildToolVersion

    defaultConfig {
        applicationId buidlConfig.applicationId
        multiDexEnabled true

        //sdks
        minSdkVersion buidlConfig.min
        targetSdkVersion buidlConfig.target

        //Version config
        versionName String.format('%d.%d', versionConfig.versionMajor, versionConfig.versionMinor)
        versionCode versionConfig.versionCode

        //strip other than english resources
        resConfigs "en"

        testInstrumentationRunner "com.kevalpatel2106.testutils.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), proguardConfig.proguardPath, proguardConfig.removeLogs
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.playStore
            shrinkResources true
            pseudoLocalesEnabled false
            zipAlignEnabled true

            buildConfigField "String", "BASE_URL", "\"" + SERVER_BASE_URL + "\""
            resValue "string", "application_name", "Stand Up"
            resValue "string", "api_host", "\"" + SERVER_BASE_URL.replace("https://", "") + "\""
            resValue "string", "server_client_id", SERVER_CLIENT_ID
            resValue "string", "facebook_app_id", FACEBOOK_APP_ID
            resValue "string", "android_api_key", ANDROID_API_KEY
        }
        debug {
            crunchPngs false
            testCoverageEnabled false   //https://stackoverflow.com/a/47050632/4690731
            debuggable true
            jniDebuggable true
            renderscriptDebuggable false
            signingConfig signingConfigs.playStore
            minifyEnabled false
            zipAlignEnabled true

            buildConfigField "String", "BASE_URL", "\"" + SERVER_BASE_URL + "\""
            resValue "string", "application_name", "Stand Up"
            resValue "string", "api_host", "\"" + SERVER_BASE_URL.replace("https://", "") + "\""
            resValue "string", "server_client_id", SERVER_CLIENT_ID
            resValue "string", "facebook_app_id", FACEBOOK_APP_ID
            resValue "string", "android_api_key", ANDROID_API_KEY
        }
    }

    compileOptions compileOption
    packagingOptions packageExcludes
    lintOptions lintOption
    aaptOptions aaptOption
    dexOptions dexOption
    testOptions testOption
    adbOptions adbOption
}

dokka dokkaConfig
kotlin kotlinOption

dependencies {
    implementation project(':base')
    implementation project(':utils')
    implementation project(':network')
    implementation project(':facebook-auth')
    implementation project(':google-auth')
    implementation project(':data-vault')
    implementation project(':ruler-view')
    implementation project(':engine')
    testImplementation project(':test-utils')
    androidTestImplementation project(':test-utils')

    implementation ide.kotlin
    implementation ide.coroutine
    implementation ide.constrainlayout

    //Butter knife
    implementation(butterKnifeLib.butterKnife, excludeGrp)
    kapt(butterKnifeLib.annotationProcessor, excludeGrp)

    //Loggers
    debugImplementation(debugLibs.dbdebug, excludeGrp)
    debugImplementation(debugLibs.leakcanary, excludeGrp)
    releaseImplementation(debugLibs.crashalytics) {
        transitive = true
    }
    implementation(firebaseLib.preformance, excludeGrp)
    implementation(debugLibs.timber, excludeGrp)

    //Support
    implementation supportLib.appCompat
    implementation supportLib.supportv4
    implementation(supportLib.recyclerView, excludeGrp)
    implementation(supportLib.cardView, excludeGrp)
    implementation supportLib.multidex
    implementation supportLib.design

    //Arch component
    implementation archComponent.lifecycle
    kapt archComponent.lifecycleAnotations

    //Firebase
    implementation(firebaseLib.core, excludeGrp)
    implementation(firebaseLib.fcm, excludeGrp)
    implementation(firebaseLib.remoteConfig, excludeGrp)

    //Rx
    implementation rxLib.rxJava
    implementation(rxLib.rxAndroid, excludeGrp)

    //Other libs
    implementation(otherLib.bottomsheet, excludeGrp)
    implementation(otherLib.circleImagView, excludeGrp)
    implementation(otherLib.glide, excludeGrp)
    kapt(otherLib.glideAnnotation, excludeGrp)
}
apply plugin: 'com.google.gms.google-services'