/*
 *  Copyright 2018 Keval Patel.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 */

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
        maven { url 'http://repo1.maven.org/maven2' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath(Plugins.buildTools)
        classpath(Plugins.jacoco)
        classpath(Plugins.dexcount)
        classpath(Plugins.versionScan)
        classpath(Plugins.googleServices)
        classpath(Plugins.firbase)
        classpath(Plugins.fabric)
        classpath(Plugins.kotlin)
        classpath(Plugins.dokka)
        classpath(Plugins.testLogger)

        if (System.getenv('CI') == "true") {
            classpath(Plugins.buildScan)
        }
    }

}

apply from: './gradle/config.gradle'
apply plugin: 'com.github.ben-manes.versions'

if (System.getenv('CI') == "true") {
    apply plugin: "com.gradle.build-scan"

    buildScan {
        licenseAgreementUrl = 'https://gradle.com/terms-of-service'
        licenseAgree = 'yes'

        publishAlways()
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        maven { url 'http://repo1.maven.org/maven2' }
    }

    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

subprojects {
    project.configurations.all {
        //https://github.com/flutter/flutter/issues/14020
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex')) {
                details.useVersion Versions.supportLib
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// See: https://medium.com/@passsy/use-different-build-numbers-for-every-build-automatically-using-a-gradle-script-35577cd31b19
// import the script which runs the version generation
apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'
